#!/usr/bin/perl -w

use strict;

use File::Temp qw( tempfile tempdir );

if (my $dir=shift) {
  chdir $dir or die $!;
}

opendir DIR,"." or die $!;

my @files=grep /^[^\.\n][^\n]*$/,
  readdir(DIR)
    or die $!;

my ($tfh,$tfn) = tempfile()
  or die "couldn't create tempfile()S!?!?";

print $tfh "$_\n" for @files;

length($ENV{EDITOR})
  or die "no EDITOR environment set";

system($ENV{EDITOR},$tfn)
  and die "editor failure";

unlink $tfn
  or die $!;

seek $tfh,0,0;

my @new_files=<$tfh>;

chomp(@new_files);

close $tfh
  or die $!;

scalar @files == scalar @new_files
  or die "lost or gained something";

my ( %rename, %orig, %dest, %stat, $tricky );

while (my $f = pop @files ) {
    my $n = pop @new_files;
    if ( $f ne $n ) {
      $rename{$n}=$f;
      ++$dest{$n};
      ++$orig{$f};
    } else {
      ++$stat{$f};
    }
}

for my $file (keys %dest) {
  $dest{$file}>1
    and die "non-unique renames";
  $stat{$file}
    and die "new file would overwrite existing file";
  $orig{$file}
    and $tricky=1;
}

my $tdir;

if ($tricky) {
  $tdir = tempdir();
  for (keys %rename) {
    rename($rename{$_},"$tdir/$rename{$_}")
      or die $!;
    $rename{$_}="$tdir/$rename{$_}";
  }
} 
  
while ( my ($n,$f) = each %rename ) {
  rename($f,$n)
    or die "$!\nrename($f,$n)\n";
}

if ($tdir) {
  rmdir($tdir)
    or die $!;
}
