#!/usr/bin/perl -w

use strict;

use File::Temp qw( tempfile tempdir );

my $dir=shift || ".";

chdir $dir or die $!;

opendir DIR,"." or die $!;

my @files=grep /^[^\.\n][^\n]*$/, readdir(DIR) or die $!;

my ($tfh,$tfn) = tempfile() or die "couldn't create tempfile()S!?!?";

print $tfh "$_\n" for @files;

system($ENV{EDITOR},$tfn) and die "editor failure";

unlink $tfn or die $!;

seek $tfh,0,0;

my @new_files=<$tfh>;

chomp(@new_files);

close $tfh or die $!;

die "lost or gained something\n"
  unless ( scalar @files == scalar @new_files );

my ( @renames, %orig, %dest, %stat, $tricky );

while (my $f = pop @files ) {
    my $n = pop @new_files;
    if ( $f ne $n ) {
      push @renames,$f,$n;
      ++$dest{$n};
      ++$orig{$f};
    } else {
      ++$stat{$f};
    }
}

for my $file (keys %dest) {
  die "non-unique renames" if $dest{$file}>1;
  die "new file would overwrite existing file" if $stat{$file};
  $tricky=1 if $orig{$file};
}

if ($tricky) {
  die "tricky";
} else {
  while (@renames) {
    my ($f,$n) = splice(@renames,0,2); 
    rename($f,$n) or die $!;
  }
}
